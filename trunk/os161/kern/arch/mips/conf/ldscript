/*
 * This is a pile of crap that tells the linker how to link the kernel,
 * because it's too stupid to be able to work it out on its own.
 */
ENTRY(__start)

SECTIONS
{
	/*
	 * Read-only loaded sections.
	 */
	.text : { *(.text) }	/* code */
	_etext = .;		/* linker-provided symbol for end of code */

	.rodata : { *(.rodata) }	/* read-only data */	
	.reginfo : { *(.reginfo) }	/* MIPS register usage blather */

	/* don't need this, can't write-protect text */
	/* . = . + 0x1000; */

	/*
	 * Read-write loaded sections.
	 */
	
	.data : { *(.data) }	/* initialized data */
	.bss : { *(.bss) }	/* cleared-to-zero data */
	_end = .;		/* linker-provided symbol for end of program */

	/*
	 * Debug info
	 */

	/* stabs debug sections */
	.stab 0:		{ *(.stab) }
	.stabstr 0:		{ *(.stabstr) }

	/* DWARF debug sections.
	Symbols in the DWARF debugging sections are relative to the beginning
	of the section so we begin them at 0.  */
	/* DWARF 1 */
	.debug 0:		{ *(.debug) }
	.line 0:		{ *(.line) }
	/* GNU DWARF 1 extensions */
	.debug_srcinfo 0:	{ *(.debug_srcinfo) }
	.debug_sfnames 0:	{ *(.debug_sfnames) }
	/* DWARF 1.1 and DWARF 2 */
	.debug_aranges 0:	{ *(.debug_aranges) }
	.debug_pubnames 0:	{ *(.debug_pubnames) }
	/* DWARF 2 */
	.debug_info 0:		{ *(.debug_info .gnu.linkonce.wi.*) }
	.debug_abbrev 0:	{ *(.debug_abbrev) }
	.debug_line 0:		{ *(.debug_line) }
	.debug_frame 0:		{ *(.debug_frame) }
	.debug_str 0:		{ *(.debug_str) }
	.debug_loc 0:		{ *(.debug_loc) }
	.debug_macinfo 0:	{ *(.debug_macinfo) }
	/* SGI/MIPS DWARF 2 extensions */
	.debug_weaknames 0:	{ *(.debug_weaknames) }
	.debug_funcnames 0:	{ *(.debug_funcnames) }
	.debug_typenames 0:	{ *(.debug_typenames) }
	.debug_varnames 0:	{ *(.debug_varnames) }

	/* These must appear regardless of  .  */
	.gptab.sdata : { *(.gptab.data) *(.gptab.sdata) }
	.gptab.sbss : { *(.gptab.bss) *(.gptab.sbss) }
}
